plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
}

allprojects {

    wrapper {
        gradleVersion = '4.10.3'
        distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
    }

    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

subprojects {

    apply plugin: 'java'

    group 'events.segfault.gdansk2019'
    version '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        compile(
                [group: 'com.google.guava', name: 'guava', version: '27.0.1-jre']
        )

        compileOnly(
                [group: 'org.projectlombok', name: 'lombok', version: '1.18.6'],
                [group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26']
        )

        runtimeOnly(
                [group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26']
        )

        annotationProcessor(
                [group: 'org.projectlombok', name: 'lombok', version: '1.18.6']
        )

        testCompile(
                [group: 'junit', name: 'junit', version: '4.12'],
                [group: 'org.mockito', name: 'mockito-core', version: '2.24.5'],
                [group: 'org.assertj', name: 'assertj-core', version: '3.12.1']
        )
    }
}

project(':legacy') {

}

project(':beer-case') {

    apply plugin: 'groovy'
    apply plugin: 'io.spring.dependency-management'

    compileGroovy.options.encoding = 'UTF-8'
    compileTestGroovy.options.encoding = 'UTF-8'

    configurations {
        compile.exclude group: 'ch.qos.logback'
        compile.exclude module: 'spring-boot-starter-tomcat'
    }

    dependencies {
        compile(
                [group: 'io.vavr', name: 'vavr', version: '0.9.3'],
                [group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.1.3.RELEASE'],
                [group: 'org.springframework.boot', name: 'spring-boot-starter-jetty', version: '2.1.3.RELEASE']
        )

        annotationProcessor(
                [group: 'io.vavr', name: 'vavr-match-processor', version: '0.9.3']
        )

        testCompile(
                [group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.5.6'],
                [group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.5'],
                [group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.3.RELEASE']
        )
    }
}