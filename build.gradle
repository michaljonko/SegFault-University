allprojects {

    wrapper {
        gradleVersion = '4.10.3'
        distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
    }

    apply plugin: 'idea'
    apply plugin: 'eclipse'
}

subprojects {

    apply plugin: 'java'

    group 'events.segfault.gdansk2019'
    version '1.0-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
        compileOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'

        runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'

        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.6'
    }
}

project(':parser') {

    apply plugin: 'antlr'

    dependencies {
        antlr group: 'org.antlr', name: 'antlr4', version: '4.7.1'
    }

    generateGrammarSource {
        maxHeapSize = "64m"
        arguments += ["-visitor",
                      "-listener"
        ]
    }
}

project(':legacy') {

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.24.5'
    }
}

